plugins {
    id 'java'
    id 'io.papermc.paperweight.userdev' version '2.0.0-beta.16'
    id 'xyz.jpenilla.resource-factory-bukkit-convention' version '1.2.0'
    id 'com.gradleup.shadow' version '9.0.0-beta11'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://reposilite.appleptr16.com/appleptr16' }
    maven { url = 'https://repo.codemc.io/repository/maven-public/' }
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url = "https://maven.enginehub.org/repo/" }
}

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

project.group = groupValue
project.version = versionValue

configurations.configureEach {
    resolutionStrategy.cacheDynamicVersionsFor 1, 'seconds'
    resolutionStrategy.cacheChangingModulesFor 1, 'seconds'
}


dependencies {
    // Base server Paper and Spigot
    paperweight.paperDevBundle('1.21.4-R0.1-SNAPSHOT')

    // Minecraft Libraries
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.12.2'

    // appleptr16 Libraries
    compileOnly 'mc.pmc:voltpluginmanager:3.2-SNAPSHOT'
    compileOnly 'mc.apple:AppleMCUtil:1.0-SNAPSHOT'
    compileOnly 'voltskiya.utilities:VoltskiyaUtilities:2.0-SNAPSHOT'
    compileOnly 'voltskiya.mob:VoltskiyaMobManager:2.0-SNAPSHOT'
}

bukkitPluginYaml {
    name.set 'VoltskiyaCustomMobs'
    version.set versionValue
    main.set 'apple.voltskiya.custom_mobs.VoltskiyaPlugin'
    apiVersion.set mcVersion
    depend.addAll 'VoltPluginManager', 'AppleMCUtil', 'VoltskiyaUtilities', 'VoltskiyaMobManager'
    authors.add 'appleptr16'
}


shadowJar {
    archiveClassifier.set ''
    manifest {
        attributes 'Multi-Release': true
    }
    mergeServiceFiles()
    outputs.upToDateWhen { false }
    doLast {
        copy {
            from shadowJar.getArchiveFile()
            into file(serverBuildDir)
        }
    }
}
